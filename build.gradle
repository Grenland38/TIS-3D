buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'idea'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

apply plugin: 'net.minecraftforge.gradle'

def getGitRef() {
    try {
        final def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (final Throwable ignored) {
        return 'unknown'
    }
}

final def semver = "${version_major}.${version_minor}.${version_patch}"

String build_number = System.getenv('PROMOTED_NUMBER')
if (build_number == null)
    build_number = System.getenv('BUILD_NUMBER')
if (build_number == null)
    build_number = getGitRef()

version = "${semver}+${build_number}"
group = 'li.cil.tis3d'
archivesBaseName = 'tis3d'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    maven { url 'https://maven.cil.li/' } // MarkdownManual
    maven { url 'https://dvs1.progwml6.com/files/maven' } // JEI
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly fg.deobf("li.cil.markdown_manual:MarkdownManual-MC${minecraft_version}-Forge:${manual_version}:api")
    runtimeOnly fg.deobf("li.cil.markdown_manual:MarkdownManual-MC${minecraft_version}-Forge:${manual_version}")

    compileOnly fg.deobf("mezz.jei:jei-${jei_minecraft_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_minecraft_version}:${jei_version}")
}

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            mods {
                tis3d {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            mods {
                tis3d {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'

            args '--mod', 'tis3d', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources')

            mods {
                tis3d {
                    source sourceSets.main
                }
            }
        }
    }
}

task copyGeneratedResources(type: Copy) {
    from 'src/generated'
    into 'src/main'
    exclude 'resources/.cache'
}

jar {
    finalizedBy 'reobfJar'

    manifest {
        attributes([
            'Specification-Title'     : 'tis3d',
            'Specification-Vendor'    : 'Sangar',
            'Specification-Version'   : '1',
            'Implementation-Title'    : project.name,
            'Implementation-Version'  : "${semver}",
            'Implementation-Vendor'   : 'Sangar',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task apiJar(type: Jar) {
    archiveClassifier.set('api')
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'li/cil/tis3d/api/**'
}

artifacts {
    archives apiJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
        }
    }
    repositories {
        maven {
            url System.getenv('MAVEN_PATH')
        }
    }
}

curseforge {
    apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''
    project {
        id = curse_project_id
        releaseType = curse_project_releaseType
        changelogType = 'markdown'
        changelog = file('changelog.md')
        addGameVersion 'Forge'
        addGameVersion minecraft_version
        addGameVersion 'Java 8'
        relations {
            requiredDependency 'markdownmanual'
        }
    }
}

idea {
    module {
        for (final String exclude in ['run', 'out', 'logs', 'src/generated']) {
            excludeDirs += file(exclude)
        }
    }
}
